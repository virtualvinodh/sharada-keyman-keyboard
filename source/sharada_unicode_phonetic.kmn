store(&VERSION) '10.0'
store(&NAME) 'Sharada Unicode Phonetic'
store(&COPYRIGHT) '© 2021 Vinodh Rajan'
store(&KEYBOARDVERSION) '1.0'
store(&TARGETS) 'windows macosx linux web androidphone androidtablet mobile desktop tablet'
store(&BITMAP) 'grantha_phonetic.ico'

begin Unicode > use(main)

group(main) using keys

c Vowels

+ "a" > '𑆃'
+ "A" > '𑆄'
'𑆃' + "a" > '𑆄'
+ "i" > '𑆅'
+ "I" > '𑆆'
'𑆅' + "i" > '𑆆'
+ "R" > '𑆉'
+ "u" > '𑆇'
'𑆇' + "u" > '𑆈'
+ "U" > '𑆈'
'𑆉' + "u" > '𑆊'
+ "W" > '𑆋'
"𑆋" + "u"> '𑆌'
+ "e" > '𑆍'
'𑆃' + "i" > '𑆎'
+ "o" > '𑆏'
'𑆃' + "u" > '𑆐'

c Shift Free Vocalic vowels
'𑆫𑆶' + ';' > '𑆉'
'𑆫𑆷' + ';' > '𑆊'
'𑆬𑆶' + ';' > '𑆋'
'𑆬𑆷' + ';' > '𑆌'

c Combining Signs
+ "M" > '𑆁' c Anusvara
'𑆀' + ";" > '𑇏' c Anusvara Inverted
+ "H" > '𑆂' c Visarga
+ "~" > '𑆀' c Candrabindu

c shift free Anusvara, Visarga, Avagraha etc. 
'𑆩𑇀' + ';' > '𑆁' c Anusvara
'𑆲𑇀' + ';' > '𑆂' c Visarga
'𑆃' + ';' > '𑇁' c Avagraha
'𑆁' + ';' >  '𑆀' c Candrabindu

c Kahsmiri Specific
'𑆍' + ";" > '𑆍ᳮ'
'𑆏' + ";" > '𑆃ᳯ' 
"𑆄" +  ';'  >  "𑆃᳐"
"𑆃᳐" + ";" > "𑆄᳐"
'𑆇' + ';' > "𑆃᳘"
'𑆈' + ';' > '𑆃᳕'

c Consonants
+ "k" > '𑆑' '𑇀'
'𑆑' '𑇀' + "h" > '𑆒' '𑇀'
+ "g" > '𑆓'  '𑇀'
'𑆓' '𑇀' + "h" > '𑆔' '𑇀'
+ "G" > '𑆕' '𑇀'

+ "c" > '𑆖' '𑇀'
'𑆖' '𑇀' + "h" > '𑆗' '𑇀'
+ "j" > '𑆘' '𑇀'
'𑆘' '𑇀' + "h" > '𑆙' '𑇀'
+ "J" > '𑆚' '𑇀'

+ "T" > '𑆛' '𑇀'
'𑆛' '𑇀' + "h" > '𑆜' '𑇀'
+ "D" > '𑆝' '𑇀'
'𑆝' '𑇀' + "h" > '𑆞' '𑇀'
+ "N" > '𑆟' '𑇀'

+ "t" > "𑆠" '𑇀'
'𑆠' '𑇀' + "h" > '𑆡' '𑇀'
+ "d" > '𑆢' '𑇀'
'𑆢' '𑇀' + "h" > '𑆣' '𑇀'
+ "n" > '𑆤' '𑇀'

+ "p" > '𑆥' '𑇀'
'𑆥' '𑇀' + "h" > '𑆦' '𑇀'
+ "b" > '𑆧' '𑇀'
'𑆧' '𑇀' + "h" > '𑆨' '𑇀'
+ "m" > '𑆩' '𑇀'

+ "y" >'𑆪'  '𑇀'
+ "r" > '𑆫' '𑇀'
+ "l" > '𑆬' '𑇀'
 '𑆬' '𑇀' + ';' > '𑆭' '𑇀'
+ "L" > '𑆭' '𑇀'
+ "v" > '𑆮' '𑇀'
+ "S" > '𑆰' '𑇀'
+ "s" > '𑆱' '𑇀'
'𑆱' '𑇀' + "h" > '𑆯' '𑇀' c sh
+ "h" > '𑆲' '𑇀'

c Nukta based

+ 'f' > '𑆦𑇊𑇀'
+ 'z' > '𑆘𑇊𑇀'
+ 'Z' > '𑆭𑇊𑇀'

'𑇀' + "q" > '𑇊' '𑇀'

c Shift free consonants

'𑆠' '𑇀' + ";" > '𑆛' '𑇀' c TT
'𑆢' '𑇀' + ";" > '𑆝' '𑇀' c DD
'𑆤' '𑇀' + ";" > '𑆟' '𑇀' c NN
'𑆤𑇀𑆓𑇀' + ';' > '𑆕' '𑇀' c nk
'𑆤𑇀𑆘𑇀' + ';' > '𑆚' '𑇀' c nj
'𑆯' '𑇀' + ';' >  '𑆰' '𑇀' c ss

c Sub ha

'&' + 'h' > '𑆲' '𑇀'

c Combining Signs

'𑇀' + "a" > nul c Remove Virama

store( cons ) '𑆑𑆒𑆓𑆔𑆕𑆖𑆗𑆘𑆙𑆚𑆛𑆜𑆝𑆞𑆟𑆠𑆡𑆢𑆣𑆤𑆥𑆦𑆧𑆨𑆩𑆪𑆫𑆬𑆮𑆯𑆰𑆱𑆲𑆭𑇊'

any(cons) + 'a' > index(cons,1) '𑆳' c -aa
'𑇀' + "A" > '𑆳' c -aa

'𑇀' + "i" > '𑆴' c -i
'𑆴' + "i" > '𑆵' c -ii
'𑇀' + "I"> '𑆵' c -ii

'𑇀' + "u" > '𑆶' c -u
'𑆶' + "u" > '𑆷' c -uu
'𑇀' + "U"> '𑆷' c -uu

'𑇀' + "R" > '𑆸' c -voc r
'𑆸' + "u" > '𑆹' c -voc rr
'𑇀' + "W" > '𑆺' c -voc l
'𑆺' + "u" > '𑆻' c -voc ll

'𑇀' + "e" > '𑆼' c -e
any(cons) + "i" >  index(cons, 1) '𑆽' c -ai
'𑇀' + "o" > '𑆾' c  o
any(cons) + "u" > index(cons, 1) '𑆿' c -au

c Shift Free vocalics
'𑇀𑆫𑆶' + ';' > '𑆸' c -voc r
'𑇀𑆫𑆷' + ';' > '𑆹' c -voc rr
'𑇀𑆬𑆶' + ';' > '𑆺' c -voc l
'𑇀𑆬𑆷' + ';' > '𑆻' c -voc ll

c Kashmiri Specific

"𑆼" + ";" > "ᳮ"
"𑆾" + ";" > "ᳯ"
"𑆳" + ";" > "᳐"
"᳐" + ";" > "𑆳᳐"
"𑆶" + ";" > "᳘"
"𑆷" + ";" > "᳕"

c Jihvamliya & Upadhmaniya 

+ "K" > "𑇂"
"𑆑𑇀" + ";" > "𑇂"
+ "P" > "𑇃"
"𑆥𑇀" + ";" > "𑇃"

c Prshita matra e

"ᳮ" + ";" > "𑇎"

c Signs
+ "@" > '𑇁' c Avagraha
'O' + 'M' > "𑆇𑆀" c OM

c Shift Free Pluta and OM
'𑆏𑆩𑇀𑆩𑇀' + ';' > "𑆇𑆀" c OM

c Numerals

+ "0" > U+111D0
+ "1" > U+111D1
+ "2"   > U+111D2
+ "3"   > U+111D3
+ "4"   > U+111D4
+ "5"   > U+111D5
+ "6"   > U+111D6
+ "7"   > U+111D7
+ "8"   > U+111D8
+ "9"   > U+111D9

c Vedic Signs

'𑆓𑇀' + "M" > 'ꣳ' c Agama Anusvara
'ꣳ' + "M" > 'ꣴ' c Dirgha Agama Anusvara

c Shift Free

'𑆓𑇀𑆩𑇀' + ';' > 'ꣳ' c Agama Anusvara
'ꣳ𑆩𑇀' + ';' >  'ꣴ'  c Dirgha Agama Anusvara

c Devanagari Block

+ "." > "𑇅" c Danda
"𑇅"+ '.' > "𑇆" c Danda double

c Vedic Svaras

"''" + ';' > "᳚" c Dirga svarita
'"' + ";" > "᳚" c Dirgha svarita
"'" + ";" > "॑" c Svarita
'_' + ";" > U+0952 c Anudatta
U+0952 + ';' > "᳜" c kathaka anudatta
"᳜" + ';' > "᳝" c dot below
"᳝" + ';' > "᳨" c anudatta with tail below

c Joiners

'𑇀' + ":" > U+200D '𑇀' c sub-base
U+200D '𑇀' + ":" > '𑇀' U+200C c prevent

c remove joiner for ha
U+200D '𑇀' + 'h' > '𑇀𑆲'

c Sharada Vowel diacritics

'#' + ';' > '𑇇'
'/' + ';' > '𑇈'
':' + ';' > '𑇉'
"॑" + ';' > '𑇋'
'!' + ';' > '𑇌'

c Sharada characters

'E' + 'K' > '𑇚' c ekam
'𑆱𑆴' + ';' > '𑇛' c Siddham
'-' + ';' > '𑇜' c empty head stroke
',' + ';' > '𑇝' c continuation
'=' + ';' > '𑇞' c section mark 1
'^' + ';' > '𑇟' c section mark 2
'𑆱𑆷' + ';' > '𑇍' c Siddham

